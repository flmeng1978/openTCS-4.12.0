buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    // AsciiDoctor plugin(s)
    classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11'
    classpath 'org.asciidoctor:asciidoctorj-diagram:1.5.0'
    // License plugin
    classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1'
    // Versions plugin
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
    // Stats plugin
    classpath 'org.kordamp.gradle:stats-gradle-plugin:0.2.0'
    // Swagger generator plugin for the web API documentation
    classpath "gradle.plugin.org.hidetake:gradle-swagger-generator-plugin:2.11.0"
  }
}

apply plugin: 'base' // To add "clean" task to the root project.
apply plugin: 'distribution'
//'base'、'distribution'是core plugins provided by Gradle itself
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'opentcs-gradle-plugin'

apply from: "${rootDir}/gradle/versioning.gradle"

group = 'org.opentcs' //group分项目的所在组的信息

subprojects {
  //apply from：从其它文件中引入gradle脚本
  apply from: rootProject.file('gradle/common.gradle')
}
evaluationDependsOnChildren() //通常情况下都是先配置父项目再配置子项目，这句改变这个顺序

distributions {
  main {
    distributions
    contents.from {
      project(':openTCS-Kernel').ext.collectableDistDir
    }
    contents.from {
      project(':openTCS-KernelControlCenter').ext.collectableDistDir
    }
    contents.from {
      project(':openTCS-PlantOverview').ext.collectableDistDir
    }
//    contents.from {
//      project(':openTCS-Documentation').ext.collectableDistDir
//    }
  }
}

task statsAggregate(type: org.kordamp.gradle.stats.AggregateStatsReportTask) {
  dependsOn subprojects*.stats
}

task subDists {
  dependsOn(':openTCS-Kernel:installDist')
  dependsOn(':openTCS-KernelControlCenter:installDist')
  dependsOn(':openTCS-PlantOverview:installDist')
//  dependsOn(':openTCS-Documentation:installDist')
}

//subprojects*的语法来源？
//子项目中没有install定义，是指子项目的文件夹路径吗？
//build了一下，发现有没有这句没啥变化
task install {
  dependsOn subprojects*.install 
}

//installDist来自Distribution Plugin
//installDist任务创建了build/install目录
installDist.dependsOn subDists

distZip {
  classifier = 'bin'
  dependsOn subDists
}

distTar {
  classifier = 'bin'
  dependsOn subDists
  compression = Compression.GZIP
}

task distSrcZip(type: Zip) {
  classifier = 'src'
  from "${rootDir}"

  includes << 'buildSrc/**'
  includes << 'config/**'
  includes << 'gradle/**'
  includes << 'openTCS-*/**'
  includes << 'src/**'
  includes << '.nb-gradle-properties'
  includes << '*.gradle'
  includes << 'gradlew'
  includes << 'gradlew.bat'

  excludes << '.gitlab'
  excludes << '.gradle'
  excludes << '**/build'
  excludes << 'buildSrc/.gradle'
}

artifacts {
  //来自Base Plugin
  //A standard configuration for the production artifacts of a project. 
  //This results in an uploadArchives task for publishing artifacts attached to
  //the archives configuration.
  archives distZip //这句和distZip好像都执行了一个东西
  archives distSrcZip
}

build {
  dependsOn subprojects*.build
  dependsOn installDist
}

task release {
  dependsOn build
  dependsOn subprojects*.release
  dependsOn distZip
  dependsOn distSrcZip
}

//包装器，使没有安装gradle的机器能够自动下载定义的gradle版本进行工程构建
task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
  gradleVersion = '4.5'
}